(* Scenario of ABCDH protocol as [2] using idemix primitives as specified in [1]
 * MINIMAL VERSION ABCDH
 * Lucca Hirschi
 * 29/03/2016
 * Status: Done & Works
 *)

(*
IDEMIX (from [1] A formal Model of Identity Mixer Mixer (Modersheim + IBM Zurich)
             [2] A secure channel for attribute-based credentials (Alpar, Hoepman (Netherlands)))
[1]
  - users that have each a master secret never revealed to other parties
  - users are known to organizations under pseudonyms bound to sessions
  - organizations make statements abouth users by issuing credentials bound to the master secret of that user
[2]
  - selective discolsure
    SD(O,Attr,R)   "I know a credential bounds to a MS I know that has been signed by O and asserting
                    that I have attribute Attre. This is bound to R".
*)
(* THEORY - INGREDIENTS
- Master Secret: Each user u has a master secret MS(u).
- Pseudonym: Each user u may generate a random R and use pseudonym pd(u,R).
- Commitments: commit(R,V) is comit of V with random V
- Credentials: basically a list of attributes signed by an issuing organization.
   We assume each orga sign a certain type of attribute and its public keys are publicly known
   -> cred(sk(O),xU,Attr): credential for attribute Attr by orga O bound to master secret xu (of user u)
   Can be issued using some sk(O), and some commit(R,xu), A.
To simplify, we only consider attributes verification that are unary relations. For instance "over-18 years old".
This will be an attribute "isOver18".
For SD:
SD(cred(sk(O),xU,Attr),O, xU, R, Attr)
   check: if it has this form or this one (for SD(\emptyset;n)): SD(cred(sk(O),xU,Attr),empty, xU, R, empty)
   projection: of O, R and Attr (not from cred)
*)

(***************)
(* ==THEORY==  *)
(***************)

type b.
channel c.

fun MS(bitstring):bitstring [private].  (* Master secret of a user *)
fun sk(bitstring):bitstring [private].  (* Secret key of an organization O *)
fun commit(bitstring,bitstring):bitstring.      (* commitments (public but no projection) *)
fun skS(bitstring):bitstring.           (* Secret key of signature of credentials *)
fun cred(bitstring,bitstring,bitstring):bitstring.      (* credential (public, projection3) *)
fun SD(bitstring,bitstring,bitstring,bitstring,bitstring):bitstring.    (* Selective Disclosure *)

fun isOver18():bitstring.
fun movieTheater():bitstring.
fun requestIsOver18():bitstring.
fun empty():bitstring.
fun accept():bitstring.
fun OK():bitstring.
fun c1():bitstring.
fun c2():bitstring.
fun g():bitstring.

fun senc(bitstring,bitstring):bitstring.
fun exp(bitstring,bitstring) :bitstring.

(* Organizations *)
fun OrgaAge():bitstring.           (* new organization: can issue age info *)
fun OrgaCheckAge():bitstring.      (* new organization: can check age info *)


(* Random Oracles: free constructor symbols *)
fun f1(bitstring):bitstring.
fun f2(bitstring):bitstring.
fun f3(bitstring):bitstring.

(* Diffie-Hellman exponentiation *)
equation forall x :bitstring, y :bitstring;
  exp(exp(g,x),y) = exp(exp(g,y),x).
(* symmetric encryption *)
reduc forall m:bitstring, k:bitstring;
      sdec(senc(m, k), k) = m.
(* SD: Selective Disclosure *)
fun verifySD(bitstring):bitstring
reduc forall O:bitstring, U:bitstring, Attr:bitstring, R:bitstring;
	verifySD(SD(cred(skS(O), MS(U), Attr), O, MS(U), R, Attr)) = accept
otherwise forall O:bitstring, U:bitstring, Attr:bitstring, R:bitstring;
	verifySD(SD(cred(skS(O), MS(U), Attr), empty, MS(U), R, empty)) = accept.
reduc forall O:bitstring, xU:bitstring, Attr:bitstring, R:bitstring, O2:bitstring, Attr2:bitstring;
        getPublicSD(SD(cred(skS(O), xU, Attr), O2, xU, R, Attr2)) = (O2,R,Attr2).

(* For defining idealizations *)
free hole:bitstring.

(****************)
(* ==PROTOCOL== *)
(****************)

let verifier(u:bitstring, Overifier:bitstring, Oprover:bitstring, credV:bitstring) =
    new xv:bitstring;
    new NEWu:bitstring;
    out(c, choice[(exp(g,xv), SD(credV, Overifier, MS(u), f1(exp(g,xv)), movieTheater)),
      	          (xv, SD(cred(skS(OrgaCheckAge),MS(NEWu),movieTheater), Overifier, MS(NEWu), f1(xv), movieTheater))
                 ]); 
    in(c,x:bitstring);
    let (xhc:bitstring,xsc:bitstring) = x in
    let (=accept) = verifySD(xsc) in
    let (=empty, =f1((exp(g,xv),xhc)), =empty) = getPublicSD(xsc) in
    out(c, choice[senc((c1,OK), f2(exp(xhc,xv))),
                  hole
		 ]);
    in(c, y:bitstring);
    new n:bitstring;
    let (=c2, =OK) = sdec(y, f2( exp(xhc,xv))) in
    out(c, choice[senc((n,requestIsOver18), f2(exp(xhc,xv))),
                  hole]);
    in(c, z:bitstring);
    let (=isOver18, xsv:bitstring) = sdec(z,f2( exp(xhc,xv))) in
    let (=accept) = verifySD(xsv) in
    let (=Oprover, =f3((n, exp(xhc,xv))), =isOver18) = getPublicSD(xsv) in   	
    out(c, OK).
    
let prover(u:bitstring, Overifier:bitstring, Oprover:bitstring, credP:bitstring) =
    in(c,x:bitstring);
    new xc:bitstring;
    new NEWu:bitstring;
    let (xhv:bitstring, xsv:bitstring) = x in
    let (=accept) = verifySD(xsv) in
    let (=Overifier, =f1(xhv), =movieTheater) = getPublicSD(xsv) in
    out(c, choice[(exp(g, xc), SD(credP, empty, MS(u), f1((xhv, exp(g, xc))), empty)),
      	          (xc, SD(cred(skS(OrgaAge),MS(NEWu),isOver18), empty, MS(NEWu), f1((hole,xc)), empty))
                 ]);
    in(c, y:bitstring);
    let (=c1, =OK) = sdec(y, f2(exp(xhv,xc))) in
    out(c, choice[senc((c2,OK), f2(exp(xhv,xc))),
                  hole]);
    in(c, z:bitstring);
    new NEWn:bitstring;
    let (xn:bitstring, =requestIsOver18) = sdec(z,f2(exp(xhv,xc))) in
    out(c, choice[senc((isOver18, SD(credP, Oprover, MS(u), f3((xn, exp(xhv,xc))), isOver18)),f2(exp(xhv,xc))),
                  hole
		  ]).

let SYSTEM =
! new userA:bitstring;      (* new user *)
  new userB:bitstring;      (* new user *)
!
(  prover(userA,
	  OrgaCheckAge,
          OrgaAge,
          cred(skS(OrgaAge),MS(userA),isOver18)
	  )
 | verifier(userB,
	    OrgaCheckAge,
            OrgaAge,
            cred(skS(OrgaCheckAge),MS(userB),movieTheater)
	   )
).	   

process SYSTEM


(* RMQ:
For the sake of readability, I replaced 'bitstring' by 'b'. To compile this file into a well formed
Proverif file, type:
     ./replace.sh irma_abcdh.ORIG  > irma_abcdh_clean.pi; ukano  irma_abcdh_clean.pi  
*)