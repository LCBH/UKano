theory ToySeqTheory
begin

/* This file models the protocol described in Example 9 from the journal
paper on UKano (JCS'19).

Description with Alice & Bob notations:
  T -> R: nT
  R -> T: nR    -- Test of inequality between message 1 and message 2
  T -> R: MAC((nR, nT), k) -- MAC Test
  R -> T: MAC((nT, nR), k) -- MAC Test

This protocol is subject to an unlinkability attack when sessions can be executed
in parallel (see toyExample9.pv in the folder ToyExample/) but Tamarin shows with
this file that it is safe otherwise

--> Tamarin concludes that Well-Authentication holds. Ukano concludes that frame opacity
    holds for a protocol that has strictly more executions (Example 15).
    Therefore, the protocol from Example 9 satisfies unlinkability.
*/

section{* ToySeq *}

	 
builtins: hashing

functions: mac/2

/****************/
/* Whole system */
/****************/

rule RepId:
 [ Fr(~k) ] --[NewId(~k)]-> [!RepSess(~k)]

rule RepSess:
 [ !RepSess(~k), Fr(~s),Fr(~nr),Fr(~nt) ]
 --[NewSess(~k, ~s), TagInit(~k, ~s, ~nt), ReaderInit(~k, ~s, ~nr) ]->
 [Tag2(~nt, ~k, ~s), Reader2(~nr, ~k, ~s)]


/*******/
/* Tag */
/*******/

rule Tout_1: // out nT
 [Tag2(~n, ~k, ~s)]
 --[PlayT(~k,~s), OutT1(~k,~s,~n)]->
 [Out(~n), Tag3(~n, ~k, ~s)]

rule Tin_1: // in nR
 [Tag3(~nt, ~k, ~s), In(x1)]
 --[Neq(x1,~nt),PlayT(~k,~s), InT1(~k,~s,x1)]->
 [Tag4(~nt, ~k, ~s, x1)]

rule Tout_2: // out mac(<nr,nt>,k)
 [Tag4(~nt, ~k, ~s, nr)]
 --[PlayT(~k,~s), OutT2(~k,~s,mac(<nr,~nt>,~k))]-> 
 [Out(mac(<nr,~nt>,~k)), Tag5(~nt, ~k, ~s, nr)]

rule Tin_2: // in mac(<nt,nr>,k)
 let x2 = mac(<~nt,nr>,~k) in
 [Tag5(~nt, ~k, ~s, nr), In(x2)]
 --[PlayT(~k,~s), InT2(~k,~s,x2), TestT2(~k,~s)]->
 [Tag6(~nt, ~k, ~s, nr)]

rule Tout_3: // out ok
 [Tag6(~nt, ~k, ~s, nr)]
 --[PlayT(~k,~s), OutT3(~k,~s,'ok'), CompleteT(~k, ~s)]->
 [Out('ok')]


/**********/
/* Reader */
/**********/

rule Rin_1: // in nt
 [Reader2(~nr, ~k, ~s), In(y1)]
 --[PlayR(~k,~s), InR1(~k,~s,y1)]->
 [Reader3(~nr, ~k, ~s, y1)]

rule Rout_1: // out nR
 [Reader3(~nr, ~k, ~s, y1)]
 --[PlayR(~k,~s), OutR1(~k,~s,~nr)]-> 
 [Out(~nr), Reader4(~nr, ~k, ~s, y1)]

rule Rin_2: // in y2 
 let y2 = mac(<~nr,n>,~k) in
 [Reader4(~nr, ~k, ~s, n), In(y2)]
 --[PlayR(~k,~s), InR2(~k,~s,y2), TestR1(~k,~s)]->
 [Reader5(~nr, ~k, ~s, n)]

rule Rout_2: // out mac(<nt,nr>,k)
 let m = mac(<nt,~nr>,~k) in
 [Reader5(~nr, ~k, ~s, nt)]
 --[PlayR(~k,~s), OutR2(~k,~s,m),CompleteR(~k,~s)]->
 [Out(m)]


/****************/
/* Restrictions */
/****************/

restriction predicate_Neq:
	"All #i a b. Neq(a,b)@i ==> not(a = b)"

// The following restriction gets rid some executions, but all those executions cannot be produced when sessions of tags are only sequentially executed
restriction seq_sessionT: // for having sessions in sequence only
	"not (Ex k s1 s2 #i1 #i2 #i3.
	        PlayT(k,s1) @ i1 & 
                PlayT(k,s2) @ i2 &
                PlayT(k,s1) @ i3 &
                i1 < i2 &
                i2 < i3 &
                not(s1=s2)
              )"

/**********/
/* Lemmas */
/**********/

/* Easy lemmas for sanity check */
lemma secrecyTag1 :
"	not( Ex k s b #t1. ((TagInit(k, s, b)@t1 & Ex #t2. K(k)@t2)))"
lemma secrecyTag2 :
"	not( Ex k s b #t1. ((TagInit(k, s, b)@t1 & Ex #t2. K(s)@t2)))"

lemma secrecyReader1 :
"	not( Ex k s a #t1 #t2. ((ReaderInit(k, s, a)@t1 &  K(k)@t2)))"
lemma secrecyReader2 :
"	not( Ex k s a #t1 #t2. ((ReaderInit(k, s, a)@t1 &  K(s)@t2)))"

lemma tagComplete: //for sanity
	exists-trace 
	"Ex #t k s. CompleteR(k,s)@t"

lemma readerComplete: //for sanity
	exists-trace
	"Ex #t k s. CompleteT(k,s)@t"


/* WA */

lemma TestT2 :
"
   All k s2 #t1.
     TestT2(k,s2)@t1 ==>
                     (Ex mT2. InT2(k,s2,mT2)@t1 &
                       Ex s1 #t9. OutR2(k,s1,mT2)@t9 &
                         Ex mR1 #t10. InR2(k,s1,mR1)@t10 &
                           Ex #t11. OutT2(k,s2,mR1)@t11 &
                             Ex mT1 #t12. InT1(k,s2,mT1)@t12 &
                               Ex #t13. OutR1(k,s1,mT1)@t13 &
                                 Ex mR0 #t14. InR1(k, s1, mR0)@t14 &
                                   Ex #t15. OutT1(k, s2, mR0)@t15 
& (t15 < t14 & t14 <t13 & t13 < t12 & t12 < t11 & t11 < t10 & t10 < t9 & t9 < t1))
"


lemma TestR1 :
"
   All k s2 #t1.
     TestR1(k,s2)@t1 ==>
                     (Ex mT2. InR2(k,s2,mT2)@t1 &
                       Ex s1 #t9. OutT2(k,s1,mT2)@t9 &
                         Ex mR1 #t10. InT1(k,s1,mR1)@t10 &
                           Ex #t11. OutR1(k,s2,mR1)@t11 &
                             Ex mT1 #t12. InR1(k,s2,mT1)@t12 &
                               Ex #t13. OutT1(k,s1,mT1)@t13
& (t13 < t12 & t12 <t11 & t11 < t10 & t10 < t9 & t9 < t1))
"

end
